apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

dependencies {

    final SUPPORT_LIBRARY_VERSION = '24.0.0'
    final VOLLEY_VERSION = '1.0.19'
    final FRESCO_VERSION = '0.10.0'
    final FASTJSON_VERSION = '1.2.12'
    final EVENTBUS_VERSION = '2.4.1'// 3.0.0
    final BUTTERKNIFE_VERSION = '8.1.0'
    final DAGGER_VERSION = '2.4'
    final RXJAVA_VERSION = '1.1.6'
    final RXANDROID_VERSION = '1.2.1'
    final RXLIFECYCLE_VERSION = '0.6.1';

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:palette-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.mcxiaoke.volley:library:$VOLLEY_VERSION"
    //    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile "com.facebook.fresco:fresco:$FRESCO_VERSION"
    compile "com.alibaba:fastjson:$FASTJSON_VERSION"
    compile "de.greenrobot:eventbus:$EVENTBUS_VERSION"
//    compile "com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar"

    // butterknife
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    // dagger2
    compile "com.google.dagger:dagger:$DAGGER_VERSION"// Dagger2
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"// APT dependencies
    provided 'org.glassfish:javax.annotation:10.0-b28'// Required by Dagger2

    // rxjava & rxandroid
    compile "io.reactivex:rxjava:$RXJAVA_VERSION"
    compile "io.reactivex:rxandroid:$RXANDROID_VERSION"

    // rxlifecycle
    compile "com.trello:rxlifecycle:$RXLIFECYCLE_VERSION"
    compile "com.trello:rxlifecycle-components:$RXLIFECYCLE_VERSION"

    // rxpermissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'

    // recyclerview item animator
    compile 'jp.wasabeef:recyclerview-animators:2.2.3'
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        // Enabling multidex support. Requires Build-Tools 21.1.0, and Support repository rev 8.
        multiDexEnabled false
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        debug {
        }
        // moves the whole sourceSet (and its sub folders) to a new folder. This moves src/androidTest/* to tests/*
        androidTest.setRoot('tests')
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Trigger BuildConfig creation
project.afterEvaluate {
    tasks.all { task ->
        if (task.name =~ /generate.*BuildConfig/) {
            task.dependsOn(propagateBuildConfig)
        }
    }
}
task propagateBuildConfig << {
    project.android.buildTypes.all { type ->
        type.buildConfigField "boolean", "RELEASE", isVariantRelease().toString()
    }
}

def isVariantRelease() {
    return rootProject.ext.variantRelease
}